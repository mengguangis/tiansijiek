<html>

<head>
<title>Chinese Painting Tile Based Deferred Shading</title>
<meta charset ="utf-8">
<meta http-equiv="X-UA-Compatible" content="chrome=1">  <!-- Use Chrome Frame in IE -->
</head>

<style type="text/css">
    * { -webkit-user-select: none; -moz-user-select: none; cursor: default; }
    body { font: 13px/18px Arial, sans-serif; background: white; color: black; overflow: hidden; }
    a { color: inherit; cursor: pointer; }
    img { display: none; }
    ul { padding: 0 0 0 20px; }
    h1 { font: bold italic 30px/30px Arial; text-align: center; }
    h2 { font: bold italic 17px/17px Arial; padding-top: 10px; }
    small { display: block; font-size: 11px; line-height: 15px; }
    canvas { position: absolute; top: 0; left: 0;}
    #help { position: absolute; top: 0; right: 0; bottom: 0; width: 20%; padding-right: 30px; overflow: auto; padding-bottom: 30px;/*background-color: black;*/}
    #help .slider{width: 90%; padding-left:10px;padding-top:5px;}
    #range{font-size:18px;font:Arial, sans-serif;}
    #info { position: absolute; top: 750px; right: 0; bottom: 0; width: 300px; padding-right: 30px; overflow: auto; }
    #loading {position:absolute; left:30%; top:50%; right:0; color:white; font-size: 30px;}
</style>

<body>
<div id="message" style="position:absolute;top:100px"></div> <!-- Pixel offset to avoid FPS counter -->
<canvas id="canvas" style="border: none; width: 75%; height: 100%" tabindex="1"></canvas>

<div id = "loading">
    <p id="loading-text" style="color: black">Initializing the awesome......</p>
</div>
<div id = "help">
    <h1><span id="title-webgl2"></span> - Tile Based Deferred Shading</h1>
    <p> Developed by <br> Sijie Tian <a href = "https://twitter.com/sijietian">twitter</a> <a href = "https://github.com/tiansijie">github</a> <br> Yuqin Shao <a href = "https://twitter.com/YuqinShao
    ">twitter</a> <a href = "https://github.com/yuqinshao">github</a> </p>
    <p>
        This demo includes <span id ="helplightnum">100</span> dynamic lights and <span id ="trianglenum">66450  </span>triangles. <br>
        You are uisng <b><span id="is-webgl2"></span><span id="helpmode">Deferred Shading</span></b>.
    </p>
    <p>
        This demo requires a decent graphics card and up-to-date drivers. If you can't run the demo, you can still see it on <a href = "https://vimeo.com/83616115">Vimeo</a>
    </p>
    <p>
    <h2>Instructions:</h2>
    <p>
        <li>Drag mouse to view around the scene</li>
        <li>w,s,a,d to move the camera</li>
        <li>Press 1 to display depth texutre</li>
        <li>Press 2 to display normal texture</li>
        <li>Press 3 to display position texutre</li>
        <li>Press 4 to display color texture</li>
        <li>Press 5 to display ambient texture</li>
        <li>Press 6 to display tile based deferred shading</li>
        <li>Press 7 to display non tile based deferred shading</li>
        <li>Press 8 to display NPR with tile based deferred shading</li>
        <li>Press 9 to display debug view for tile based</li>
        <li>Press 0 to display scissor test visualization</li>
        <li>Press f to switch between deferred shading and forward shading</li>
        <li>Modify number of lights in the scene:<span id = "range">100</span> lights</li>
        <input id="light-number-range" class = "slider" type="range" min="0" max="1000" value="100" step="5" width = "200px" onchange="showValue(this.value)"/>

    </p>

    <h2> Source Code: </h2>
    <a href = "https://github.com/tiansijie/Tile_Based_WebGL_DeferredShader">Code</a>

</div>


<script id="pass_vs" type="x-shader/x-vertex">
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    attribute vec3 Position;
    attribute vec3 Normal;
    attribute vec2 Texcoord;

    varying vec3 fs_Normal;
    varying vec4 fs_Position;
    varying vec2 fs_Texcoord;
    varying float fs_Depth;

    void main(void) {
        fs_Normal = ((u_InvTrans*vec4(Normal,0.0)).xyz);
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        fs_Position = camera;
        gl_Position = u_Persp * camera;
        fs_Texcoord = Texcoord;
        fs_Depth = ((gl_Position.z / gl_Position.w));
    }
</script>

<script id="forward_fs" type="x-shader/x-fragment">
    precision highp float;

    uniform vec3 u_Color;
    uniform vec3 light_pos;
    uniform vec3 light_color;
    uniform float light_radius;
    uniform int drawmode;
    uniform vec4 u_Light;

    //in
    varying vec3 fs_Normal;
    varying vec4 fs_Position;
    varying vec2 fs_Texcoord;
    varying float fs_Depth;

    uniform sampler2D u_Texutre;

    void main(void)
    {

        vec3 color = texture2D(u_Texutre, fs_Texcoord).xyz;
        vec3 position = fs_Position.xyz;
        vec3 normal = normalize(fs_Normal);

        if(drawmode == 1){
           if(distance(light_pos, position) < light_radius){

                float dist = distance(light_pos, position);
                float lightR = light_radius;
                float attenuation = dist/(1.0 - (dist/lightR) * (dist/lightR));
                attenuation = attenuation / lightR + 1.0;
                attenuation = 1.0 / (attenuation * attenuation);

                float diffuse = abs(dot(normal, normalize(light_pos)));
                gl_FragColor = vec4(diffuse * light_color * color * attenuation, 1.0);
            }
            else
                gl_FragColor = vec4(vec3(0.0),1.0);
        }
        else
        {
            vec3 light = u_Light.xyz;
            float strength = u_Light.w;

            float ambient = 0.09;
            float diffuse = abs(dot(normalize(light),(normal)));

            gl_FragColor = vec4(color*(strength*diffuse + ambient),1.0);
        }
    }
</script>


<script id="pass_fs" type="x-shader/x-fragment">
    #extension GL_EXT_draw_buffers : require
    precision highp float;

    uniform vec3 u_Color;
    //in
    varying vec3 fs_Normal;
    varying vec4 fs_Position;
    varying vec2 fs_Texcoord;
    varying float fs_Depth;

    uniform sampler2D u_Texutre;

    void main(void)
    {
        //normal, position, depth, color
        gl_FragData[0] = vec4(vec3(fs_Depth), 1.0);
        gl_FragData[1] = vec4(normalize(fs_Normal.xyz), 1.0);
        gl_FragData[2] = fs_Position;
        gl_FragData[3] = vec4(texture2D(u_Texutre, fs_Texcoord).xyz, 1.0);
    }
</script>



<script id="webgl2_pass_vs" type="x-shader/x-vertex">#version 300 es
    precision highp float;

    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_InvTrans;

    in vec3 Position;
    in vec3 Normal;
    in vec2 Texcoord;

    out vec3 fs_Normal;
    out vec4 fs_Position;
    out vec2 fs_Texcoord;
    out float fs_Depth;

    void main(void) {
        fs_Normal = ((u_InvTrans*vec4(Normal,0.0)).xyz);
        vec4 world = u_Model * vec4(Position, 1.0);
        vec4 camera = u_View * world;
        fs_Position = camera;
        gl_Position = u_Persp * camera;
        fs_Texcoord = Texcoord;
        fs_Depth = ((gl_Position.z / gl_Position.w));
    }
</script>

<script id="webgl2_pass_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;

    uniform vec3 u_Color;
    //in
    in vec3 fs_Normal;
    in vec4 fs_Position;
    in vec2 fs_Texcoord;
    in float fs_Depth;

    uniform sampler2D u_Texutre;
    out vec4 fragColor[4];

    void main(void)
    {
        //normal, position, depth, color
        fragColor[0] = vec4(vec3(fs_Depth), 1.0);
        fragColor[1] = vec4(normalize(fs_Normal.xyz), 1.0);
        fragColor[2] = fs_Position;
        fragColor[3] = vec4(texture(u_Texutre, fs_Texcoord).xyz, 1.0);
    }
</script>

<script type="x-shader/x-fragment" id = "withoutdrawbufferfs">
    precision mediump float;

    uniform int u_DrawMode;

    varying vec3 fs_Normal;
    varying vec4 fs_Position;
    varying vec2 fs_Texcoord;
    varying float fs_Depth;

    uniform sampler2D u_Texutre;


    void main(void) {
        if(u_DrawMode == 0)
        {
            gl_FragColor = vec4(normalize(fs_Normal),0.0);
            return;
        }
        else if(u_DrawMode == 1)
        {
            //diaplay color
            gl_FragColor = vec4(texture2D(u_Texutre, fs_Texcoord).xyz, 1.0);
            return;
        }
        else if(u_DrawMode == 2)
        {
            //display screen space position
            gl_FragColor = vec4(fs_Position.xyz,1.0);
            return;
        }
    }
</script>


<script id="webgl2_shade_vs" type="x-shader/x-vertex">#version 300 es
precision highp float;

in vec3 Position;
in vec2 Texcoord;

out vec2 fs_Texcoord;

void main() {
		fs_Texcoord = Texcoord * 0.5 + vec2(0.5);
		gl_Position = vec4(Position,1.0);
}
</script>

<script id="shade_vs" type="x-shader/x-vertex">
    precision highp float;

    attribute vec3 Position;
    attribute vec2 Texcoord;

    varying vec2 fs_Texcoord;

    void main() {
        fs_Texcoord = Texcoord * 0.5 + vec2(0.5);
        gl_Position = vec4(Position,1.0);
    }
</script>


<script id="light_fs" type="x-shader/x-fragment">
    precision highp float;


    #define DISPLAY_DEPTH 0
    #define DISPLAY_NORMAL 1
    #define DISPLAY_POSITION 2
    #define DISPLAY_COLOR 3
    #define DISPLAY_TOTAL 4
    #define DISPLAY_LIGHTS 5
    #define DISPLAY_NONTILE_LIGHTS 6
    #define DISPLAY_INK 7
    #define DISPLAY_DEBUGTILE 8
    #define MAXLIGHTNUM 1000


    uniform sampler2D u_Depthtex;
    uniform sampler2D u_Normaltex;
    uniform sampler2D u_Positiontex;
    uniform sampler2D u_Colortex;

    //for light
    uniform sampler2D u_LightGridtex;
    uniform sampler2D u_LightIndextex;
    uniform sampler2D u_LightPositiontex;
    uniform sampler2D u_LightColorRadiustex;
    uniform int u_LightNum;

    uniform int u_DisplayType;
    uniform float u_Far;
    uniform float u_Near;
    uniform float u_Width;
    uniform float u_Height;
    uniform int u_MaxTileLightNum;

    uniform int u_TileSize;
    uniform int u_LightIndexImageSize;
    uniform float u_FloatLightIndexSize;
    uniform float u_WidthTile;
    uniform float u_HeightTile;

    varying vec2 fs_Texcoord;

    float linearizeDepth(float exp_depth, float near, float far) {
        return  (2.0 * near) / (far + near -  exp_depth * (far - near));
    }



    void main(void)
    {
        vec3 normal = texture2D(u_Normaltex, fs_Texcoord).xyz;
        vec3 position = texture2D(u_Positiontex, fs_Texcoord).xyz;
        vec3 color = texture2D(u_Colortex, fs_Texcoord).xyz;

        if(u_DisplayType == DISPLAY_LIGHTS || u_DisplayType == DISPLAY_INK || u_DisplayType == DISPLAY_DEBUGTILE){
            //get the grid data index
            vec2 gridIndex = vec2(((fs_Texcoord.x*u_Width) / float(u_TileSize)) / (u_WidthTile), ((fs_Texcoord.y*u_Height) / float(u_TileSize)) / (u_HeightTile));
            //x for offset, y for count
            vec3 gridInfo = texture2D(u_LightGridtex, gridIndex).xyz;
            int offset = int(gridInfo.x);
            int count = int(gridInfo.y);
            int offset2 = int(gridInfo.z);

            vec3 lightColor = vec3(0.0);
            int lightCount = 0;
            if(count > 0){
                for(int i = 0; i < MAXLIGHTNUM; i++){
                    if(i < count){

                        int lightId;
                        float temp = float(offset + i);
                        int addNext = 0;

                        if(temp >= u_FloatLightIndexSize){
                            temp -= u_FloatLightIndexSize;
                            addNext++;
                        }


                        //precision problem
                        if(temp == u_FloatLightIndexSize)
                           temp = 0.0;

                        float lightSize =  u_FloatLightIndexSize-1.0;

                        vec2 dataIndex = vec2(
                            temp / lightSize,
                            float(offset2 + addNext) / lightSize
                            );

                        lightId = int((texture2D(u_LightIndextex, dataIndex).x));

                        vec3 lightPos = texture2D(u_LightPositiontex, vec2(float(lightId)/float(u_LightNum-1))).xyz;
                        vec4 lightColorRadius = texture2D(u_LightColorRadiustex, vec2(float(lightId)/float(u_LightNum-1))).xyzw;

                        if(distance(lightPos, position) < lightColorRadius.w){

                            float diffuse = abs(dot(normal, normalize(lightPos - position)));

                            float dist = distance(lightPos, position);
                            float lightR = lightColorRadius.w;
                            float attenuation = dist/(1.0 - (dist/lightR) * (dist/lightR));
                            attenuation = attenuation / lightR + 1.0;
                            attenuation = 1.0 / (attenuation * attenuation);

                            if(u_DisplayType == DISPLAY_INK){
                                vec3 quaColor = vec3(0.0);
                                if(diffuse<0.4){
                                    quaColor = vec3(0.4);
                                }
                                else if(diffuse < 0.6){
                                    quaColor = vec3(0.6);
                                }
                                else if(diffuse < 0.9){
                                    quaColor = vec3(0.78);
                                }
                                else if(diffuse < 1.0){
                                    quaColor = vec3(0.89);
                                }
                                else
                                {
                                    quaColor = vec3(1.0,0.0,1.0);
                                }



                                lightColor += quaColor * attenuation * lightColorRadius.xyz * diffuse * color; //* ((lightColorRadius.w - distance(lightPos, position)) / lightColorRadius.w);// * lightColorRadius.xyz;
                            }
                            else
                            {
                                lightColor += diffuse * lightColorRadius.xyz * attenuation* color;// * ((lightColorRadius.w - distance(lightPos, position)) /     lightColorRadius.w);
                            }
                            lightCount ++;
                        }
                    }
                    else
                        break;
                }
                if(u_DisplayType == DISPLAY_DEBUGTILE){
                   lightColor = vec3(float(count) / float(u_MaxTileLightNum)) ;
                }
                else
                    lightColor = mix(vec3(0.0), vec3(1.0), lightColor);

                lightColor = clamp(lightColor, vec3(0.0), vec3(1.0));
            }
            gl_FragColor = vec4(lightColor, 1.0);
        }
        else
            gl_FragColor = vec4(vec3(0.0), 1.0);
    }
</script>


<script id="webgl2_nontilelight_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;

    #define DISPLAY_DEPTH 0
    #define DISPLAY_NORMAL 1
    #define DISPLAY_POSITION 2
    #define DISPLAY_COLOR 3
    #define DISPLAY_TOTAL 4
    #define DISPLAY_TILE_LIGHTS 5
    #define DISPLAY_NONTILE_LIGHTS 6
    #define DISPLAY_SCISSOR -1

    uniform int u_DisplayType;
    uniform sampler2D u_Depthtex;
    uniform sampler2D u_Normaltex;
    uniform sampler2D u_Positiontex;
    uniform sampler2D u_Colortex;

    uniform vec4 u_Light;
    uniform vec3 u_LightColor;

    in vec2 fs_Texcoord;
    out vec4 fragmentColor;

    void main()
    {
        vec3 normal = texture(u_Normaltex, fs_Texcoord).xyz;
        vec3 position = texture(u_Positiontex, fs_Texcoord).xyz;
        vec3 color = texture(u_Colortex, fs_Texcoord).xyz;
        vec3 light = u_Light.xyz;
        float lightRadius = u_Light.w;


        if(u_DisplayType == DISPLAY_NONTILE_LIGHTS){
            if(distance(light, position) < lightRadius){
                float dist = distance(light, position);
                float lightR = lightRadius;
                float attenuation = dist/(1.0 - (dist/lightR) * (dist/lightR));
                attenuation = attenuation / lightR + 1.0;
                attenuation = 1.0 / (attenuation * attenuation);

                float diffuse = abs(dot(normal, normalize(light - position)));
                fragmentColor = vec4(diffuse * u_LightColor * color * attenuation, 1.0);
            }
            else
                fragmentColor = vec4(0.0,0.0,0.0,1.0);
        }
        else if(u_DisplayType == DISPLAY_SCISSOR){
            float dis = distance(light, position) * 2.0;
            fragmentColor = vec4(1.0/dis,0.0,0.0, 1.0);
        }
        else
            fragmentColor = vec4(0.0,0.0,0.0, 1.0);
    }
</script>

<script id="nontilelight_fs" type="x-shader/x-fragment">
    precision highp float;

    #define DISPLAY_DEPTH 0
    #define DISPLAY_NORMAL 1
    #define DISPLAY_POSITION 2
    #define DISPLAY_COLOR 3
    #define DISPLAY_TOTAL 4
    #define DISPLAY_TILE_LIGHTS 5
    #define DISPLAY_NONTILE_LIGHTS 6
    #define DISPLAY_SCISSOR -1

    uniform int u_DisplayType;
    uniform sampler2D u_Depthtex;
    uniform sampler2D u_Normaltex;
    uniform sampler2D u_Positiontex;
    uniform sampler2D u_Colortex;

    uniform vec4 u_Light;
    uniform vec3 u_LightColor;

    varying vec2 fs_Texcoord;

    void main()
    {
        vec3 normal = texture2D(u_Normaltex, fs_Texcoord).xyz;
        vec3 position = texture2D(u_Positiontex, fs_Texcoord).xyz;
        vec3 color = texture2D(u_Colortex, fs_Texcoord).xyz;
        vec3 light = u_Light.xyz;
        float lightRadius = u_Light.w;


        if(u_DisplayType == DISPLAY_NONTILE_LIGHTS){
            if(distance(light, position) < lightRadius){
                float dist = distance(light, position);
                float lightR = lightRadius;
                float attenuation = dist/(1.0 - (dist/lightR) * (dist/lightR));
                attenuation = attenuation / lightR + 1.0;
                attenuation = 1.0 / (attenuation * attenuation);

                float diffuse = abs(dot(normal, normalize(light - position)));
                gl_FragColor = vec4(diffuse * u_LightColor * color * attenuation, 1.0);
            }
            else
                gl_FragColor = vec4(0.0,0.0,0.0,1.0);
        }
        else if(u_DisplayType == DISPLAY_SCISSOR){
            float dis = distance(light, position) * 2.0;
            gl_FragColor = vec4(1.0/dis,0.0,0.0, 1.0);
        }
        else
            gl_FragColor = vec4(0.0,0.0,0.0, 1.0);
    }
</script>


<script id="diagnostic_fs" type="x-shader/x-fragment">
    precision highp float;

    #define DISPLAY_DEPTH 0
    #define DISPLAY_NORMAL 1
    #define DISPLAY_POSITION 2
    #define DISPLAY_COLOR 3
    #define DISPLAY_TOTAL 4
    #define DISPLAY_LIGHTS 5

    uniform sampler2D u_Depthtex;
    uniform sampler2D u_Normaltex;
    uniform sampler2D u_Positiontex;
    uniform sampler2D u_Colortex;

    uniform int u_DisplayType;
    uniform float u_Far;
    uniform float u_Near;
    uniform float u_Width;
    uniform float u_Height;

    uniform vec4 u_Light;

    varying vec2 fs_Texcoord;

    float linearizeDepth(float exp_depth, float near, float far) {
        return  (2.0 * near) / (far + near -  exp_depth * (far - near));
    }

    void main(void)
    {
        vec3 depth = texture2D(u_Depthtex, fs_Texcoord).xyz;

        float exp_depth = texture2D(u_Depthtex, fs_Texcoord).r;
        float lin_depth = linearizeDepth(exp_depth,u_Near,u_Far);

        vec3 normal = normalize(texture2D(u_Normaltex, fs_Texcoord).xyz);
        vec3 position = texture2D(u_Positiontex, fs_Texcoord).xyz;
        vec3 color = texture2D(u_Colortex, fs_Texcoord).xyz;


        vec3 light = u_Light.xyz;//.position.xyz;
        //u_Light.xyz;
        float diffuse = abs(dot(normal, normalize(light - position)));

        if(u_DisplayType == DISPLAY_DEPTH)
            gl_FragColor = vec4(vec3(lin_depth) * 30.0, 1.0);
        else if(u_DisplayType == DISPLAY_NORMAL)
            gl_FragColor = vec4(abs(normal), 1.0);
        else if(u_DisplayType == DISPLAY_POSITION)
            gl_FragColor = vec4(abs(position)/u_Far * 120.0, 1.0);
        else if(u_DisplayType == DISPLAY_COLOR){
            gl_FragColor = vec4(color, 1.0);
        }
        else if(u_DisplayType == DISPLAY_TOTAL)
        {
            gl_FragColor = vec4(diffuse * color , 1.0);
        }
        else
            gl_FragColor = vec4(vec3(0.0), 1.0);
    }
</script>


<script id="ambient_fs" type="x-shader/x-fragment">
    precision highp float;

    uniform sampler2D u_Depthtex;
    uniform sampler2D u_Normaltex;
    uniform sampler2D u_Positiontex;
    uniform sampler2D u_Colortex;

    uniform int u_DisplayType;

    uniform vec4 u_Light;

    varying vec2 fs_Texcoord;

    void main(void)
    {
        vec3 depth = texture2D(u_Depthtex, fs_Texcoord).xyz;
        vec3 normal = normalize(texture2D(u_Normaltex, fs_Texcoord).xyz);
        vec3 position = texture2D(u_Positiontex, fs_Texcoord).xyz;
        vec3 color = texture2D(u_Colortex, fs_Texcoord).xyz;

        vec3 light = u_Light.xyz;
        float strength = u_Light.w;

        float ambient = 0.09;
        float diffuse = abs(dot(normalize(light),(normal)));

        gl_FragColor = vec4(color*(strength*diffuse + ambient),1.0);
    }
</script>


<script id="post_vs" type="x-shader/x-vertex">
    precision highp float;

    attribute vec3 Position;
    attribute vec2 Texcoord;

    varying vec2 fs_Texcoord;

    void main() {
        fs_Texcoord = Texcoord * 0.5 + vec2(0.5);
        gl_Position = vec4(Position,1.0);
    }

</script>


<script id="post_fs" type="x-shader/x-fragment">
    precision highp float;

    #define s2(a,b)     temp = a; a= min(a,b); b = max(temp,b);
    #define mn3(a, b, c)            s2(a, b); s2(a, c);
    #define mx3(a, b, c)            s2(b, c); s2(a, c);
    #define mnmx3(a, b, c)          mx3(a, b, c); s2(a, b);                                   // 3 exchanges
    #define mnmx4(a, b, c, d)       s2(a, b); s2(c, d); s2(a, c); s2(b, d);                   // 4 exchanges
    #define mnmx5(a, b, c, d, e)    s2(a, b); s2(c, d); mn3(a, c, e); mx3(b, d, e);           // 6 exchanges
    #define mnmx6(a, b, c, d, e, f) s2(a, d); s2(b, e); s2(c, f); mn3(a, b, c); mx3(d, e, f); // 7 exchanges


    #define DISPLAY_DEPTH 0
    #define DISPLAY_NORMAL 1
    #define DISPLAY_POSITION 2
    #define DISPLAY_COLOR 3
    #define DISPLAY_TOTAL 4
    #define DISPLAY_LIGHTS 5
    #define DISPLAY_NONTILE_LIGHTS 6
    #define DISPLAY_INK 7

    uniform sampler2D u_Posttex;
    uniform sampler2D u_StrokeBlurtex;

    uniform int u_DisplayType;

    uniform float u_Width;
    uniform float u_Height;

    varying vec2 fs_Texcoord;
    varying vec4 out_Color;

    vec3 MedianFilter(sampler2D sampler)
    {
        vec2 imageSize = vec2(float(u_Width),float(u_Height));
        vec2 tex_inv_size = 1.0 / imageSize;
        vec3 v[9];
        for(int dx = -1; dx <=1; ++dx)
        {
            for(int dy = -1; dy <=1; ++dy)
            {
                vec2 offset = vec2(float(dx),float(dy));
                v[(dx+1)*3 + (dy+1)] = texture2D(sampler,fs_Texcoord+offset * tex_inv_size).rgb;
            }
        }
        vec3 temp;
        // Starting with a subset of size 6, remove the min and max each time
        mnmx6(v[0], v[1], v[2], v[3], v[4], v[5]);
        mnmx5(v[1], v[2], v[3], v[4], v[6]);
        mnmx4(v[2], v[3], v[4], v[7]);
        mnmx3(v[3], v[4], v[8]);
        return v[4];
    }


    vec3 colorEnhance(float min,float max,vec3 color)
    {
        if(color.r == 0.0)
        {
            return vec3(0.97,0.94,0.85);
        }
        if(color.r > max)
        {
            color = vec3(1.0);
        }
        else if(color.r <= max && color.r >= min)
        {
            color = vec3((color.r - min) / (max - min));
        }
        else if(color.r < min)
        {
            color = vec3(0.0);
        }
        return color;
    }


    void main(void)
    {
        if(u_DisplayType == DISPLAY_INK){
            vec3 spattercolor = MedianFilter(u_Posttex);
            spattercolor = colorEnhance(0.2,0.98,spattercolor);

            vec3 strokecolor = texture2D(u_StrokeBlurtex,fs_Texcoord).rgb;

            if(strokecolor.x != 1.0 || strokecolor.y != 1.0 || strokecolor.z != 1.0)
            {
               gl_FragColor = vec4(min(spattercolor,strokecolor),1.0);
            }
            else
            {
                gl_FragColor = vec4(spattercolor,1.0);
            }
        }
        else
        {
            vec3 out_Color = texture2D(u_Posttex, fs_Texcoord).xyz;
            gl_FragColor = vec4(out_Color, 1.0);
        }
    }

</script>


<script type="x-shader/x-fragment" id = "spatterfs">
    precision mediump float;

    #define DISPLAY_DEPTH 0
    #define DISPLAY_NORMAL 1
    #define DISPLAY_POSITION 2
    #define DISPLAY_COLOR 3
    #define DISPLAY_TOTAL 4
    #define DISPLAY_LIGHTS 5
    #define DISPLAY_NONTILE_LIGHTS 6
    #define DISPLAY_INK 7

    uniform int u_DisplayType;

    uniform sampler2D u_QuatColorSampler;
    uniform int u_Width;
    uniform int u_Height;

    float rand(vec2 n,float radius)
    {

        return radius * fract(sin(dot(n.xy, vec2(12.9898, 78.233)))*43758.5453);

    }

    varying vec2 fs_Texcoord;
    float radius = 0.01;
    void main(void)
    {
        if(u_DisplayType == DISPLAY_INK){
            vec2 pixelIndex = vec2(fs_Texcoord.x * (float(u_Width) - 1.0),fs_Texcoord.y * (float(u_Height) - 1.0));
            float randnum = rand(fs_Texcoord,radius);
            pixelIndex += vec2(randnum,0.0);
            vec2 newTexcoord = vec2(pixelIndex.x/(float(u_Width)-1.0),pixelIndex.y / (float(u_Height)-1.0));
            float randnum2 = rand(newTexcoord,radius);
            pixelIndex += vec2(0,randnum2);
            newTexcoord = vec2(pixelIndex.x/(float(u_Width)-1.0),pixelIndex.y / (float(u_Height)-1.0));
            vec4 newColorSample = texture2D(u_QuatColorSampler,newTexcoord);
            newColorSample.w = 1.0;
            gl_FragColor = newColorSample;
        }
        else
        {
            vec3 out_Color = texture2D(u_QuatColorSampler, fs_Texcoord).rgb;
            gl_FragColor = vec4(out_Color, 1.0);
        }

        return;
    }
</script>

<script type="x-shader/x-vertex" id = "siledgevs">
    uniform mat4 u_Model;
    uniform mat4 u_View;
    uniform mat4 u_Persp;
    uniform mat4 u_Inverse;

    attribute vec3 Position;

    varying vec4 vPosition;
    varying float vDepth;

    void main(void) {
        vec4 world = u_Model * vec4(Position,1.0);
        vec4 camera = u_View * world;
        vPosition = camera;
        gl_Position = u_Persp * camera;
        vDepth = gl_Position.z / gl_Position.w;
    }
</script>


<script type="x-shader/x-fragment" id = "siledgefs">
    precision mediump float;
    uniform vec3 u_Color;

    varying vec4 vPosition;
    varying float vDepth;

    float linearizeDepth(float exp_depth, float near, float far) {
        return  (2.0* near) / (far + near -  exp_depth * (far - near));
    }

    void main(void) {
        float linDepth = linearizeDepth(vDepth,0.1,10.0);

        gl_FragData[0] = vec4(1.0,0.0,0.0,1.0);
    }
</script>


<script type="x-shader/x-fragment" id = "silCullfs">
    precision mediump float;

    uniform sampler2D u_DepthSampler;
    uniform sampler2D u_SilColorSampler;
    uniform sampler2D u_SilDepthSampler;
    uniform sampler2D u_DepthSamplerFake;

    float linearizeDepth(float exp_depth, float near, float far) {
        return  (2.0* near) / (far + near -  exp_depth * (far - near));
    }

    varying vec2 fs_Texcoord;

    void main(void)
    {
        float exp_depth = texture2D(u_DepthSampler,fs_Texcoord).r;
        float lin_depth = linearizeDepth(exp_depth,0.1,10.0);
        float silDepth = texture2D(u_SilDepthSampler,fs_Texcoord).r;
        vec4 sil = texture2D(u_SilColorSampler,fs_Texcoord);

        float depthColorfake = texture2D(u_DepthSamplerFake,fs_Texcoord).r;
        float linDepth = linearizeDepth(depthColorfake,0.1, 10.0);
        float originDepth = linDepth;

        if(abs(silDepth - originDepth) <= 0.009)
        {
            gl_FragColor = vec4(0.0,0.0,0.0,1.0);//vec4(0.0,0.0,0.0,1.0)* sil;
            return;
        }
        else
        {
            gl_FragColor = vec4(1.0);
            return;
        }
    }
</script>


<script type="x-shader/x-fragment" id ="strokefs">
    precision mediump float;
    uniform sampler2D u_SilColorSampler;
    uniform int u_viewportWidth;
    uniform int u_viewportHeight;

    varying vec2 fs_Texcoord;
    void main(void)
    {
        vec4 color = texture2D(u_SilColorSampler,fs_Texcoord);

        int numofColoredPixel = 0;
        int hasPixel = 0;
        vec4 tempColor = vec4(0.0);
        if(color.x == 1.0 && color.y == 1.0 && color.z == 1.0 && color.w == 1.0)
        {
            vec2 pixelIndex = vec2(fs_Texcoord.x * (float(u_viewportWidth)),fs_Texcoord.y * (float(u_viewportHeight)));
            for(int i = -2; i<=2 ;++i)
            {
                for(int j = -2; j<=2; ++j)
                {
                    if(i == 0 && j == 0)
                        continue;
                    vec2 newpixelIdx = pixelIndex + vec2(i,j);
                    if(newpixelIdx.x < float(u_viewportWidth) && newpixelIdx.x > 0.0 && newpixelIdx.y < float(u_viewportHeight) && newpixelIdx.y > 0.0)
                    {
                        vec4 newColor = texture2D(u_SilColorSampler,vec2(newpixelIdx.x / (float(u_viewportWidth)),
                                                            newpixelIdx.y / (float(u_viewportHeight))));
                        if(newColor.x == 0.0 && newColor.y == 0.0 && newColor.z == 0.0 && newColor.w == 1.0)
                        {
                          numofColoredPixel ++;
                        }
                    }
                }
            }
            if(numofColoredPixel  >= 1)
            {
                color = vec4(0.0,0.0,0.0,1.0);
            }

        }

        gl_FragColor = color;
        return;
    }

</script>

<script type="x-shader/x-fragment" id ="strokeblurfs">
    precision mediump float;
    uniform sampler2D u_StrokeSampler;
    uniform int u_viewportWidth;
    uniform int u_viewportHeight;

    varying vec2 fs_Texcoord;
    void main(void)
    {

        vec4 color = texture2D(u_StrokeSampler,fs_Texcoord);

        vec2 pixelIndex = vec2(fs_Texcoord.x * (float(u_viewportWidth) - 1.0),fs_Texcoord.y * (float(u_viewportHeight) - 1.0));
        vec4 colorAccu = vec4(0.0,0.0,0.0,1.0);
        for(int i = -3; i<=3 ;++i)
        {
            for(int j = -3; j<=3; ++j)
            {
                vec2 newpixelIdx = pixelIndex + vec2(i,j);
                vec4 newColor = texture2D(u_StrokeSampler,vec2(newpixelIdx.x / (float(u_viewportWidth)-1.0),
                                                    newpixelIdx.y / (float(u_viewportHeight)-1.0)));
                colorAccu += newColor;
            }
        }

        color = colorAccu / 49.0;
        gl_FragColor = color;
        return;
    }
</script>

<script type="x-shader/x-fragment" id ="edgefs">
    precision highp float;

    #define DISPLAY_DEPTH 0
    #define DISPLAY_NORMAL 1
    #define DISPLAY_POSITION 2
    #define DISPLAY_COLOR 3
    #define DISPLAY_TOTAL 4
    #define DISPLAY_LIGHTS 5

    uniform sampler2D u_Depthtex;
    uniform sampler2D u_Normaltex;
    uniform sampler2D u_Positiontex;
    uniform sampler2D u_Colortex;
    uniform sampler2D u_QuatColorSampler;

    uniform int u_DisplayType;
    uniform float u_Far;
    uniform float u_Near;
    uniform float u_Width;
    uniform float u_Height;

    uniform vec4 u_Light;

    varying vec2 fs_Texcoord;

    void main() {
        vec3 depth = texture2D(u_Depthtex, fs_Texcoord).xyz;
        vec3 normal = texture2D(u_Normaltex, fs_Texcoord).xyz;
        vec3 position = texture2D(u_Positiontex, fs_Texcoord).xyz;
        vec3 color = texture2D(u_QuatColorSampler, fs_Texcoord).xyz;

        vec3 light = u_Light.xyz;
        float strength = u_Light.w + 0.3;

        float ambient = 0.09;

        mat3 GX, GY;
        GX[0] = vec3(1.0,2.0,1.0);
        GX[1] = vec3(0.0,0.0,0.0);
        GX[2] = vec3(-1.0,-2.0,-1.0);

        GY[0] = vec3(1.0,0.0,-1.0);
        GY[1] = vec3(2.0,0.0,-2.0);
        GY[2] = vec3(1.0,0.0,-1.0);

        vec3 magX = vec3(0.0);
        vec3 magY = vec3(0.0);

        for(int i = 0;i < 3; i++){
            for(int j = 0; j < 3; j++){
                float mx = (float(j-1)) / float(u_Width);
                float my = (float(i-1)) / float(u_Height);

                vec2 nearTexcoord = vec2(fs_Texcoord.s + mx, fs_Texcoord.t + my);

                vec3 nearNrm = texture2D(u_Normaltex, nearTexcoord).xyz;
                float nearDiffuse = max(0.0, dot(normalize(light),nearNrm));
                vec3 nearColor = texture2D(u_QuatColorSampler, nearTexcoord).xyz;
                nearColor = nearColor*(strength*nearDiffuse + ambient);

                float c = length(nearColor);

                magX += vec3(GX[j][i]) * nearColor;
                magY += vec3(GY[j][i]) * nearColor;
            }
        }

        vec3 mag = sqrt((magX)*(magX) + (magY)*(magY));

         if(mag.x < 0.1 && mag.y < 0.1 && mag.z < 0.1)
         {
            gl_FragColor = vec4(vec3(1.0), 1.0);
         }
         else
         {
            gl_FragColor = vec4(vec3(0.0), 1.0);
         }
    }
</script>


<script type="text/javascript" src="./src/three/three.min.js"></script>
<script type="text/javascript" src="./src/three/OBJLoader.js"></script>
<script type="text/javascript" src="./src/three/MTLLoader.js"></script>
<script type="text/javascript" src="./src/three/OBJMTLLoader.js"></script>
<script src="./src/Stats.js" type ="text/javascript"></script>
<script src="./src/gl-matrix.js" type ="text/javascript"></script>
<script src="./src/webGLUtility.js" type ="text/javascript"></script>
<script src="./src/webglinit.js" type ="text/javascript"></script>
<script src="./src/NPRfunctions.js" type ="text/javascript"></script>
<script src="./src/lightsetup.js" type ="text/javascript"></script>
<script src="./src/deferred.js" type ="text/javascript"></script>

</body>

</html>
